buildscript {
    dependencies {
        classpath "com.h2database:h2:2.4.240"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'idea'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'application'
    id "org.jooq.jooq-codegen-gradle" version "3.19.28"
    id "org.flywaydb.flyway" version "11.19.1"
}

group = 'com.github.curiousoddman'
version = '0.0.1-SNAPSHOT'
description = 'An audio player resembling iTunes'

def javafxVersion = '25.0.1'
def testfxVersion = '4.0.18'
def tikaVersion = '3.2.3'

def flywayAndJooqDatabase ='jdbc:h2:file:~/test;DB_CLOSE_ON_EXIT=FALSE'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

run {
    jvmArgs([
            "--add-reads", "javafx.graphics=ALL-UNNAMED",
            "--add-opens", "javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--add-opens", "javafx.graphics/com.sun.javafx.iio=ALL-UNNAMED",
            "--add-opens", "javafx.graphics/com.sun.javafx.iio.common=ALL-UNNAMED",
            "--add-opens", "javafx.graphics/com.sun.javafx.css=ALL-UNNAMED",
            "--add-opens", "javafx.base/com.sun.javafx.runtime=ALL-UNNAMED"
    ])
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

javafx {
    version = "${javafxVersion}"
    modules = ['javafx.controls', 'javafx.fxml']
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'com.github.curiousoddman.curious_tunes.Main'
}

flyway {
    url = flywayAndJooqDatabase
    user = 'sa'
    password = 'sa'
    schemas = ['public']
//    placeholders = [
//            'keyABC': 'valueXYZ',
//            'otherplaceholder': 'value123'
//    ]
}

jooq {
    configuration {
        jdbc {
            driver = "org.h2.Driver"
            url = flywayAndJooqDatabase
            user = 'sa'
            password = 'sa'
        }
        generator {
            name = "org.jooq.codegen.JavaGenerator"
            database {
                inputSchema = "public"
//                excludes = "flyway_schema_history"
                //isIncludeExcludePackageRoutines = true
                schemaVersionProvider = "none"
                target {
                    packageName = "com.github.curiousoddman.curious_tunes.dbobj"
                    directory = "${layout.projectDirectory}/src/main/java"
                }
            }
        }
    }
}



jooqCodegen.dependsOn flywayMigrate

dependencies {
    runtimeOnly "org.jooq:jooq-codegen"
    runtimeOnly 'com.h2database:h2'

    implementation 'org.springframework.boot:spring-boot-h2console'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation "org.apache.tika:tika-core:${tikaVersion}"
    implementation "org.apache.tika:tika-parsers:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-audiovideo-module:${tikaVersion}"

    implementation "org.openjfx:javafx-controls:${javafxVersion}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}"

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-jooq-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    testImplementation "org.testfx:testfx-core:${testfxVersion}"
//    testImplementation "org.testfx:testfx-junit5:${testfxVersion}"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    jvmArgs('--add-opens=javafx.graphics/com.sun.javafx.application=ALL-UNNAMED')
}
